#include <Wire.h>
#include <LiquidCrystal_I2C.h>
#include <ESP8266WiFi.h>
#include <ESP8266WebServer.h>

const char* ssid = "YourWiFiSSID";     // Replace with your Wi-Fi SSID
const char* password = "YourWiFiPassword"; // Replace with your Wi-Fi Password

LiquidCrystal_I2C lcd(0x27, 16, 2); // Change address to 0x3F if 0x27 doesn't work
ESP8266WebServer server(80);

// HTML page to send text
const char* htmlPage = R"rawliteral(
  <html>
    <head><title>Smart LCD</title></head>
    <body>
      <h2>Send Text to LCD</h2>
      <form action="/send">
        <input type="text" name="message">
        <input type="submit" value="Send">
      </form>
    </body>
  </html>
)rawliteral";

void handleRoot() {
  server.send(200, "text/html", htmlPage);
}

void handleSend() {
  String message = server.arg("message");
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print(message.substring(0, 16));        // Line 1
  lcd.setCursor(0, 1);
  lcd.print(message.substring(16, 32));       // Line 2
  server.send(200, "text/html", "<h2>Message Sent!</h2><a href='/'>Back</a>");
}

void setup() {
  Serial.begin(115200);
  lcd.begin();
  lcd.backlight();

  WiFi.begin(ssid, password);
  Serial.print("Connecting to WiFi");
  while (WiFi.status() != WL_CONNECTED) {
    delay(500); Serial.print(".");
  }
  Serial.println("\nConnected: " + WiFi.localIP().toString());

  server.on("/", handleRoot);
  server.on("/send", handleSend);
  server.begin();
  Serial.println("HTTP server started");
}

void loop() {
  server.handleClient();
}
